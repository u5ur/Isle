// BuildVoxels.comp
#version 460 core
#extension GL_NV_gpu_shader5 : enable

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(binding = 0, rgba16f) coherent uniform image3D u_AtomicRadiance;
layout(binding = 1, rgba16f) coherent uniform image3D u_AtomicNormal;
layout(binding = 2, rgba16f) uniform image3D u_VoxelRadiance;
layout(binding = 3, rgba16f) uniform image3D u_VoxelNormal;
layout(binding = 4, r32ui) uniform uimage3D u_AtomicCount;

uniform ivec3 u_Resolution;

void main()
{
    ivec3 voxelCoord = ivec3(gl_GlobalInvocationID);
    
    if (any(greaterThanEqual(voxelCoord, u_Resolution)))
        return;
    
    memoryBarrierImage();
    
    vec4 radianceData = imageLoad(u_AtomicRadiance, voxelCoord);
    vec4 normalData = imageLoad(u_AtomicNormal, voxelCoord);
    uint count = imageLoad(u_AtomicCount, voxelCoord).r;
    
    vec3 finalRadiance = vec3(0.0);
    vec3 finalNormal = vec3(0.0, 0.0, 1.0);
    float alpha = 0.0;
    
    if (count > 0u)
    {
        finalRadiance = radianceData.rgb / float(count);
        finalRadiance = clamp(finalRadiance, vec3(0.0), vec3(65504.0));
        
        vec3 averagedEncodedNormal = normalData.rgb / float(count);
        finalNormal = normalize(averagedEncodedNormal * 2.0 - 1.0);
        
        float density = float(count);
        alpha = clamp(1.0 - exp(-density * 0.5), 0.0, 1.0);
    }
    
    imageStore(u_VoxelRadiance, voxelCoord, vec4(finalRadiance, alpha));
    imageStore(u_VoxelNormal, voxelCoord, vec4(finalNormal * 0.5 + 0.5, 1.0));
    
    imageStore(u_AtomicRadiance, voxelCoord, vec4(0.0));
    imageStore(u_AtomicNormal, voxelCoord, vec4(0.0));
    imageStore(u_AtomicCount, voxelCoord, uvec4(0));
}